#!/bin/tcsh
#
# take a PRIDE-PPPAR kin_* solution file and split
# it into 2 files, 1 containing just header info
# and 1 containing just solution/processed data
#
# INPUT:
#
# *_kin       PPP kinematic solution file outputted by PRIDE PPP-AR
#
# OUTPUT:
#
# *.hdr       solution file containing only the header from input kin file
# *.prd       solution file containing only positions from input kin file
#
# Originally written by tschuh-at-princeton.edu, 10/06/2021
# Last modified by tschuh-at-princeton.edu, 02/03/2022

set infile = $1
# set outfile to be og filename which is 2 directories back
# need to pipe to xargs to avoid displaying full path
set outfile = `/bin/ls -1 *.cfg | xargs -n1 basename`

set hdrend = `rnx2hdr $infile`
set hdrend = `echo "$hdrend+1" | bc`

# create hdr and prd files from original file
awk 'NR<='"$hdrend"' {print}' $infile >! $outfile:r.hdr
awk 'NR>'"$hdrend"' {print}' $infile >! $outfile:r.prd

# remove any stray asterisks (*) in the prd file
# otherwise matlab wont be able to load data
if ($OSTYPE == 'linux') then
  sed -i 's/\*/ /g' $outfile:r.prd
else if ($OSTYPE == 'darwin') then
  # on MacOS need to add '' in beginning for creation of output file
  sed -i '' 's/\*/ /g' $outfile:r.prd
else
  echo "Code did not run"
endif
